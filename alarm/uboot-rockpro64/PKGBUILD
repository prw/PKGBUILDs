# U-Boot: RockPro64
# Maintainer: Kevin Mihelich 
# Maintainer: Phil White <timberline5@gmail.com>

buildarch=8

pkgname=uboot-rockpro64
pkgver=ayufan_2019.07
pkgrel=1
pkgdesc="U-Boot for RockPro64"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git' 'rockchip-tools' 'dtc145' 'python' 'python-pyelftools')
install=${pkgname}.install
_commit_uboot=29843fbd42cdea7e2bf4e3aaa404cd39e9acf350
_commit_rkbin=430fd377b043cb51ad5f375004d4abfd0e824552
source=("git+https://github.com/ayufan-rock64/linux-u-boot.git#commit=$_commit_uboot"
        "git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        'boot.txt'
	'mkscr')
sha256sums=('SKIP'
	    'SKIP'
	    '5698bcfbf2fca73daff77d1ca490ce187b65a8b574c7500ea63a9ffd0e6d28e1'
	    'a4fc8b6b92bc364d6542670d294aa618a8501fb8729f415cc0a3eed776ef0c8e' )

build() {
  cd linux-u-boot

  unset CLFAGS CXXFLAGS CPPFLAGS LDFLAGS

  make rockpro64-rk3399_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
  make EXTRAVERSION=-${pkgrel} u-boot.itb BL31="../rkbin/bin/rk33/rk3399pro_bl31_v1.28.elf"
}

package() {
  cd linux-u-boot

  mkdir -p "${pkgdir}/boot"

  tools/mkimage -n rk3399 -T rksd -d ../rkbin/bin/rk33/rk3399_ddr_933MHz_v1.23.bin "${pkgdir}/boot/idbloader.img"
  cat spl/u-boot-spl.bin >> "${pkgdir}/boot/idbloader.img"
  dd if=u-boot.itb of="${pkgdir}/boot/idbloader.img" seek=448 conv=notrunc

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
